# *      =>  0 or more
# +      =>  1 or more
# [xyz]  =>  xyz is optional
# () are used for grouping
# anything between '' is denotes just itself, so '(' denotes an actual ( written in code

KEYWORDS: 'define' | 'var' | 'const' | 'int' | 'float' | 'string' | 'bool' | 'not' | 'and' | 'or' | 'true' | 'false' | 'array' | 'ref_of' |
    'function' | 'return' | 'if' | 'else' | 'else_if' | 'for' | 'while' | 'break' | 'continue' | 'pass'

STATEMENT: ( SIMPLE_STATEMENT | COMPOUND_STATEMENT ) [ [';'] LINE_BREAK | ';' ]

SIMPLE_STATEMENT: 'pass' | 'break' | 'continue' | RETURN_STATEMENT | OPERATOR_EXPRESSION

RETURN_STATEMENT: 'return' EXPRESSION

EXPRESSION: SIMPLE_EXPRESSION | COMPOUND_EXPRESSION

SIMPLE_EXPRESSION: NAME | LITERAL

COMPOUND_EXPRESSION: FUNCTION_CALL | CONST_VAR_DEFINITION | ARRAY_SUBSCRIPTION | OPERATOR_EXPRESSION

ARRAY_SUBSCRIPTION: NAME '[' NON_NEGATIVE_INTEGER ']'

LITERAL: NUMBER | STRING

FUNCTION_CALL: NAME '(' [ EXPRESSIONS_LIST ] ')'

EXPRESSIONS_LIST: EXPRESSION ( ',' EXPRESSION )*

OPERATOR_EXPRESSION: UNARY_OPERATOR SIMPLE_EXPRESSION | SIMPLE_EXPRESSION ( BINARY_OPERATOR SIMPLE_EXPRESSION )+

UNARY_OPERATOR: '-' | '++' | '--' | '~'

BINARY_OPERATOR: '==' | '!=' | '<' | '<=' | '>' | '>=' | 'not' | 'and' | 'or' | '+' | '-' | '*' | '/' ':=' | '=' | '+=' | '-=' | '*=' | '/=' | '~=' | '&=' | '|=' | '^=' | '>>=' | '<<='

CONST_VAR_DEFINITION: 'define' (NAME ':' TYPE ':=' EXPRESSION)+

TYPE: [ 'const' | 'var' ] ( USER_DEFINED_TYPE | PRIMITIVE_DATA_TYPE ) [ '&' ]

PRIMITIVE_DATA_TYPE: 'int' | 'float' | 'string' | 'bool' | ARRAY_TYPE

ARRAY_TYPE: 'array' [ '[' NON_NEGATIVE_INTEGER ':' TYPE ']' ]

COMPOUND_STATEMENT: FUNCTION_STATEMENT | IF_STATEMENT | FOR_STATEMENT | WHILE_STATEMENT

FUNCTION_STATEMENT: 'function' NAME '(' PARAMETERS_LIST ')' [ ':' TYPE ] BLOCK END_STATEMENT

PARAMETERS_LIST: PARAMETER ( ',' PARAMETER )*

PARAMETER: NAME ':' TYPE

BLOCK: INDENT STATEMENT+ OUTDENT

END_STATEMENT: 'end' STRING

IF_STATEMENT: 'if' EXPRESSION BLOCK [ 'else_if' BLOCK ] [ 'else' BLOCK ] END_STATEMENT

FOR_STATEMENT: 'for' NAME 'in' ARRAY_EXPRESSION BLOCK END_STATEMENT

WHILE_STATEMENT: 'while' EXPRESSION BLOCK END_STATEMENT
